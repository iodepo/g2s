PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX schema: <https://schema.org/>
PREFIX geosparql: <http://www.opengis.net/ont/geosparql#>

SELECT DISTINCT (?s AS ?id) ?g ?type ?name ?description ?contributor ?keywords
    ?variableMeasured ?provider
    ?contenturl ?courseName ?url
    ?location ?iritype ?wkt ?geom ?lat ?long ?place_name
    ?temporalCoverage ?datePublished ?dateModified
    ?sameAs ?citation ?license ?version ?includedInDataCatalog ?memberOf
    ?parentOrganization ?knowsAbout ?affiliation ?category
    ?vehicleConfiguration ?vehicleSpecialUsage
    ?jobTitle ?knowsLanguage ?educationalCredentialAwarded ?author
    ?hasCourseInstance ?areaServed ?startDate ?endDate
WHERE {
    graph ?g {
        ?s rdf:type schema:DataCatalog .
        ?s schema:description ?description .
        ?s schema:name ?name .

        BIND("DataCatalog" AS ?type)

        # Keywords as literals
        OPTIONAL {
            ?s schema:keywords ?keywords .
            FILTER (!isIRI(?keywords) && !isBlank(?keywords))
        }

        # Keywords as DefinedTerm
        OPTIONAL {
            ?s schema:keywords ?irikeywords .
            ?irikeywords a schema:DefinedTerm .
            ?irikeywords schema:name ?keywords .
            FILTER (isIRI(?irikeywords))
        }

        # Spatial coverage with latitude and longitude
        OPTIONAL {
            ?s schema:spatialCoverage ?sc .
            ?sc a schema:Place .
            ?sc schema:latitude ?lat .
            ?sc schema:longitude ?long .
        }

        # Spatial coverage with geometric data
        OPTIONAL {
            ?s schema:spatialCoverage ?sc .
            ?sc a schema:Place .
            ?sc schema:geo ?geo .
            ?geo a ?geotype .
            VALUES ?geompred { schema:box schema:polygon }
            ?geo ?geompred ?geom .
        }

        # GeoSPARQL geometry
        OPTIONAL {
            ?s geosparql:hasGeometry ?hasgeom .
            ?hasgeom geosparql:asWKT ?wkt .
        }
    }
}